# Copyright 2020 The XLS Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Yosys is a framework for RTL synthesis tools. It currently has extensive
# Verilog-2005 support and provides a basic set of synthesis algorithms for
# various application domains.

load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("@rules_python//python:defs.bzl", "py_binary")
load("@livehd//packages:yosys_utils.bzl", "yosys_bison", "yosys_flex")

licenses(["restricted"])

exports_files(["LICENSE"])

genrule(
    name = "version_src",
    outs = ["kernel/version.cc"],
    cmd = "echo 'namespace Yosys { extern const char *yosys_version_str; const char *yosys_version_str=\"Yosys livehd+0.9+\"; }' >$@",
)

cc_library(
    name = "version",
    srcs = ["kernel/version.cc"],
    copts = ["-w"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "techmap",
    srcs = [
        "techlibs/common/techmap.v",
    ],
    outs = ["passes/techmap/techmap.inc"],
    cmd = (
        "echo '// autogenerated from techmap.v' > $@;" +
        "echo 'static char stdcells_code[] = {' >> $@;" +
        "od -v -td1 -An $< | sed -e 's/[0-9][0-9]*/&,/g' >> $@;" +
        "echo '0};' >> $@"
    ),
)

# yosys_flex(
#     name = "verilog_lexer_gen",
#     srcs = ["frontends/verilog/verilog_lexer.l"],
#     outs = ["frontends/verilog/verilog_lexer.cc"],
#     args = [
#         "-o",
#         "$(execpath frontends/verilog/verilog_lexer.cc)",
#         "$(execpath frontends/verilog/verilog_lexer.l)",
#     ],
#     flex = "@flex",
# )

genrule(
    name = "verilog_lexer_gen",
    srcs = ["frontends/verilog/verilog_lexer.l"],
    outs = ["verilog_lexer.cpp"],
    cmd = "M4=$(M4) flex --outfile=$@.tmp $< && " +
        "if [ \"$$(uname)\" = \"Darwin\" ]; then " +
        "sed -e 's/int yyFlexLexer::LexerInput( char\\* buf, int/size_t yyFlexLexer::LexerInput( char* buf, size_t/' " +
        "-e 's/void yyFlexLexer::LexerOutput( const char\\* buf, int size/void yyFlexLexer::LexerOutput( const char* buf, size_t size/' " +
        "$@.tmp > $@; else cp $@.tmp $@; fi && rm $@.tmp",
    toolchains = [
        "@rules_m4//m4:current_m4_toolchain",
    ],
)

# yosys_bison(
#     name = "verilog_parser_gen",
#     srcs = ["frontends/verilog/verilog_parser.y"],
#     outs = [
#         "frontends/verilog/verilog_parser.tab.cc",
#         "frontends/verilog/verilog_parser.tab.hh",
#     ],
#     args = [
#         "-o",
#         "$(execpath frontends/verilog/verilog_parser.tab.cc)",
#         "-d",
#         "-r",
#         "all",
#         "-b",
#         "verilog_parser",
#         "$(execpath frontends/verilog/verilog_parser.y)",
#     ],
#     bison = "@bison",
# )

genrule(
    name = "verilog_parser_gen",
    srcs = ["frontends/verilog/verilog_parser.y"],
    outs = [
        "frontends/verilog/verilog_parser.tab.cc",
        "frontends/verilog/verilog_parser.tab.hh",
    ],
    cmd = "M4=$(M4) bison --defines=$(location frontends/verilog/verilog_parser.tab.hh) --output-file=$(location frontends/verilog/verilog_parser.tab.cc) $<",
    # "--name-prefix=xxx",
    toolchains = [
        "@rules_m4//m4:current_m4_toolchain",
    ],
)

py_binary(
    name = "cellhelp",
    srcs = ["techlibs/common/cellhelp.py"],
    python_version = "PY3",
    srcs_version = "PY3ONLY",
    deps = [
    ],
)

genrule(
    name = "simlib_help",
    srcs = [
        "techlibs/common/simlib.v",
    ],
    outs = ["techlibs/common/simlib_help.inc"],
    cmd = (
        "python3 $(location :cellhelp) $(SRCS) > $@"
    ),
    tools = [":cellhelp"],
)

genrule(
    name = "simcells_help",
    srcs = [
        "techlibs/common/simcells.v",
    ],
    outs = ["techlibs/common/simcells_help.inc"],
    cmd = (
        "python3 $(location :cellhelp) $(SRCS) > $@"
    ),
    tools = [":cellhelp"],
)

GENERATED_HEADERS = [
    "techlibs/ice40/ice40_dsp_pm.h",
    "techlibs/ice40/ice40_wrapcarry_pm.h",
    "passes/pmgen/test_pmgen_pm.h",
    "passes/opt/peepopt_pm.h",
    "techlibs/xilinx/xilinx_dsp_cascade_pm.h",
    "techlibs/xilinx/xilinx_dsp_CREG_pm.h",
    "techlibs/xilinx/xilinx_dsp_pm.h",
    "techlibs/xilinx/xilinx_dsp48a_pm.h",
    "techlibs/xilinx/xilinx_srl_pm.h",
    "techlibs/microchip/microchip_dsp_CREG_pm.h",
    "techlibs/microchip/microchip_dsp_cascade_pm.h",
    "techlibs/microchip/microchip_dsp_pm.h",
    "techlibs/quicklogic/ql_dsp_macc_pm.h",
]

YOSYS_COPTS = [
    "-DYOSYS_SRC='\"third_party/yosys\"'",
    "-DYOSYS_DATDIR='\"third_party/yosys\"'",
    "-fexceptions",
    "-w",
    "-Wno-shadow",
    "-Wno-implicit-fallthrough",
    "-Wno-vla",
    "-std=c++20",
]

cc_library(
    name = "kernel_include",
    hdrs = glob(
        [
            "kernel/*.h",
            "kernel/*.inc",
            "backends/**/*.h",
            "libs/**/*.hh",
            "libs/**/*.h",
            "libs/**/*.hpp",
            "frontends/**/*.h",
            "passes/**/*.h",
            "techlibs/**/*.h",
        ],
        exclude = [
            "backends/protobuf/*.h",
        ],
    ) + GENERATED_HEADERS,
    visibility = ["//visibility:public"],
    defines = [
        "_YOSYS_",
        "YOSYS_ENABLE_PLUGINS",
        # "YOSYS_ENABLE_READLINE",
        "YOSYS_ENABLE_GLOB",
        #"YOSYS_ENABLE_TCL",
        "YOSYS_ENABLE_ABC",
        "YOSYS_ENABLE_COVER",
        'ABCEXTERNAL=\\"abc\\"',
    ],
)

cc_library(
    name = "kernel",
    srcs = glob(
        [
            "kernel/*.cc",
            "backends/**/*.cc",
            "frontends/**/*.cc",
            "passes/**/*.cc",
            "techlibs/**/*.cc",
            "libs/**/*.cc"
        ],
        exclude = [
            "kernel/driver.cc",
            "backends/protobuf/*.cc",
            "frontends/verific/*.cc",
            "libs/dlfcn-win32/*.cc",
            "passes/techmap/filterlib.cc",
            "libs/**/demo*.cc",
            "libs/**/sample*.cc",
            "libs/**/test*.cc",
            "libs/ezsat/puzzle3d.cc",
            "libs/subcircuit/scshell.cc",
            "techlibs/quicklogic/testbench.cc",
            "testsuite.cc",
        ],
    ) + [
        ":verilog_lexer_gen",
        ":verilog_parser_gen",
        #":ilang_lexer",
        #":ilang_parser",
        "passes/techmap/techmap.inc",
        "techlibs/common/simcells_help.inc",
        "techlibs/common/simlib_help.inc",
        ":peepopt_pm_h",
        ":test_pmgen_pm_h",
        ":ql_dsp_macc_pm_h",
    ] + [
        ":ice40_%s_pm_h" % pm for pm in ["dsp", "wrapcarry"]
    ] + [
        ":xilinx_%s_pm_h" % pm for pm in ["dsp", "dsp48a", "dsp_CREG", "dsp_cascade", "srl"]
    ] + [
        ":microchip_%s_pm_h" % pm for pm in ["dsp", "dsp_CREG", "dsp_cascade"]
    ],
    #features = ["-use_header_modules"],
    copts = YOSYS_COPTS,
    includes = [
        ".",
        "backends/ilang",
        "frontends/ilang",
        "frontends/rtlil",
        "frontends/verilog",
        "passes/techmap",
        "techlibs/common",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":kernel_include",
        ":bigint",
        ":ezsat",
        ":json11",
        ":minisat",
        ":sha1",
        ":subcircuit",
        # "@dk_thrysoee_libedit//:pretend_to_be_gnu_readline_system",
        # "@edu_berkeley_abc//:abc-lib",
        "@abc",
        #"@edu_berkeley_abc//:abc-lib",
        "@libffi//:libffi",
        #"@tk_tcl_tcl//:tcl",
    ],
    linkopts = ["-ldl"],
    alwayslink = True,
)

cc_test(
    name = "bigint_sample",
    srcs = [
        "libs/bigint/sample.cc",
    ],
    copts = [
        "-fexceptions",
        "-w",
    ],
    features = ["-use_header_modules"],
    licenses = ["notice"],
    deps = [":bigint"],
)

cc_library(
    name = "bigint",
    srcs = [
        "libs/bigint/BigInteger.cc",
        "libs/bigint/BigIntegerAlgorithms.cc",
        "libs/bigint/BigIntegerUtils.cc",
        "libs/bigint/BigUnsigned.cc",
        "libs/bigint/BigUnsignedInABase.cc",
    ],
    hdrs = [
        "libs/bigint/BigInteger.hh",
        "libs/bigint/BigIntegerAlgorithms.hh",
        "libs/bigint/BigIntegerLibrary.hh",
        "libs/bigint/BigIntegerUtils.hh",
        "libs/bigint/BigUnsigned.hh",
        "libs/bigint/BigUnsignedInABase.hh",
        "libs/bigint/NumberlikeArray.hh",
    ],
    copts = [
        "-fexceptions",
        "-w",
    ],
    features = ["-use_header_modules"],
    licenses = ["notice"],
)

cc_test(
    name = "ezsat_demo_vec",
    srcs = [
        "libs/ezsat/demo_vec.cc",
    ],
    licenses = ["notice"],
    deps = [":ezsat"],
)

cc_test(
    name = "ezsat_demo_bit",
    srcs = [
        "libs/ezsat/demo_bit.cc",
    ],
    licenses = ["notice"],
    deps = [":ezsat"],
)

cc_test(
    name = "ezsat_demo_cmp",
    srcs = [
        "libs/ezsat/demo_cmp.cc",
    ],
    licenses = ["notice"],
    deps = [":ezsat"],
)

cc_test(
    name = "ezsat_puzzle3d",
    timeout = "eternal",
    srcs = [
        "libs/ezsat/puzzle3d.cc",
    ],
    licenses = ["notice"],
    deps = [":ezsat"],
)

cc_library(
    name = "ezsat",
    srcs = [
        "libs/ezsat/ezminisat.cc",
        "libs/ezsat/ezsat.cc",
    ],
    hdrs = [
        "libs/ezsat/ezminisat.h",
        "libs/ezsat/ezsat.h",
    ],
    copts = [
        "-fexceptions",
        "-w",
    ],
    features = ["-use_header_modules"],
    licenses = ["notice"],
    deps = [
        ":minisat",
    ],
)

cc_library(
    name = "json11",
    srcs = [
        "libs/json11/json11.cpp",
    ],
    hdrs = ["libs/json11/json11.hpp"],
    copts = ["-w"],
    includes = ["libs/json11"],
    licenses = ["notice"],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "minisat",
    srcs = [
        "libs/minisat/Options.cc",
        "libs/minisat/SimpSolver.cc",
        "libs/minisat/Solver.cc",
        "libs/minisat/System.cc",
    ],
    hdrs = [
        "libs/minisat/Alg.h",
        "libs/minisat/Alloc.h",
        "libs/minisat/Dimacs.h",
        "libs/minisat/Heap.h",
        "libs/minisat/IntMap.h",
        "libs/minisat/IntTypes.h",
        "libs/minisat/Map.h",
        "libs/minisat/Options.h",
        "libs/minisat/ParseUtils.h",
        "libs/minisat/Queue.h",
        "libs/minisat/Rnd.h",
        "libs/minisat/SimpSolver.h",
        "libs/minisat/Solver.h",
        "libs/minisat/SolverTypes.h",
        "libs/minisat/Sort.h",
        "libs/minisat/System.h",
        "libs/minisat/Vec.h",
        "libs/minisat/XAlloc.h",
    ],
    copts = [
        "-fexceptions",
        "-w",
    ],
    features = ["-use_header_modules"],
    licenses = ["notice"],
)

cc_library(
    name = "sha1",
    srcs = [
        "libs/sha1/sha1.cpp",
    ],
    hdrs = ["libs/sha1/sha1.h"],
    copts = ["-w"],
    licenses = ["notice"],
)

cc_binary(
    name = "subcircuit_shell",
    srcs = [
        "libs/subcircuit/scshell.cc",
    ],
    licenses = ["notice"],
    deps = [":subcircuit"],
)

cc_library(
    name = "subcircuit",
    srcs = [
        "libs/subcircuit/subcircuit.cc",
    ],
    hdrs = ["libs/subcircuit/subcircuit.h"],
    copts = ["-w"],
    licenses = ["notice"],
)

py_binary(
    name = "pmgen",
    srcs = ["passes/pmgen/pmgen.py"],
    python_version = "PY3",
    srcs_version = "PY3ONLY",
)

genrule(
    name = "peepopt_pm_h",
    srcs = [
        "passes/opt/peepopt_muldiv.pmg",
        "passes/opt/peepopt_muldiv_c.pmg",
        "passes/opt/peepopt_shiftmul_right.pmg",
        "passes/opt/peepopt_shiftadd.pmg",
        "passes/opt/peepopt_shiftmul_left.pmg",
        "passes/opt/peepopt_formal_clockgateff.pmg",
    ],
    outs = ["passes/opt/peepopt_pm.h"],
    # Note: pmgen.py expects the pattern name via -p (peepopt) and list of files
    cmd = "python3 $(location :pmgen) -o $(OUTS) -p peepopt $(SRCS)",
    tools = [":pmgen"],
)

genrule(
    name = "test_pmgen_pm_h",
    srcs = ["passes/pmgen/test_pmgen.pmg"],
    outs = ["passes/pmgen/test_pmgen_pm.h"],
    cmd = "python3 $(location :pmgen) -o $(OUTS) -p test_pmgen $(SRCS)",
    tools = [":pmgen"],
)

[genrule(
    name = "ice40_%s_pm_h" % pm,
    srcs = ["techlibs/ice40/ice40_%s.pmg" % pm],
    outs = ["techlibs/ice40/ice40_%s_pm.h" % pm],
    cmd = "python3 $(location :pmgen) -o $(OUTS) -p ice40_%s $(SRCS)" % pm,
    tools = [":pmgen"],
) for pm in ["dsp", "wrapcarry"]]

[genrule(
    name = "xilinx_%s_pm_h" % pm,
    srcs = ["techlibs/xilinx/xilinx_%s.pmg" % pm],
    outs = ["techlibs/xilinx/xilinx_%s_pm.h" % pm],
    cmd = "python3 $(location :pmgen) -o $(OUTS) -p xilinx_%s $(SRCS)" % pm,
    tools = [":pmgen"],
) for pm in ["dsp", "dsp48a", "dsp_CREG", "dsp_cascade", "srl"]]

[genrule(
    name = "microchip_%s_pm_h" % pm,
    srcs = ["techlibs/microchip/microchip_%s.pmg" % pm],
    outs = ["techlibs/microchip/microchip_%s_pm.h" % pm],
    cmd = "python3 $(location :pmgen) -o $(OUTS) -p microchip_%s $(SRCS)" % pm,
    tools = [":pmgen"],
) for pm in ["dsp", "dsp_CREG", "dsp_cascade"]]

genrule(
    name = "ql_dsp_macc_pm_h",
    srcs = ["techlibs/quicklogic/ql_dsp_macc.pmg"],
    outs = ["techlibs/quicklogic/ql_dsp_macc_pm.h"],
    cmd = "python3 $(location :pmgen) -o $(OUTS) -p ql_dsp_macc $(SRCS)",
    tools = [":pmgen"],
)
